//file:noinspection DifferentStdlibGradleVersion
plugins {
    id 'com.android.application'
}

def getGitHubCommit = {
    try {
        def hashOutput = new ByteArrayOutputStream()
        def changeOutput = new ByteArrayOutputStream()
        def gitVersionName = ""

        hashOutput = providers.exec {
            commandLine('git', 'rev-list', '--max-count=1', 'HEAD')
        }.standardOutput

        changeOutput = providers.exec {
            commandLine('git', 'diff-index', '--shortstat', 'HEAD')
        }.standardOutput

        gitVersionName = hashOutput.asText().get().trim().substring(0, 7)
        if (!changeOutput.asText().get().trim().empty) {
            def pattern = Pattern.compile("\\d+");
            def matcher = pattern.matcher(changeOutput.asText().get().trim())
            if (matcher.find()) {
                gitVersionName += "-" + matcher.group()
            }
        }
        return gitVersionName
    } catch (ignored) {
        return "UNKNOWN";
    }
}

android {
    compileSdk = 36
    defaultConfig {
        applicationId "uk.openvk.android.refresh"
        minSdk 21
        targetSdk 36
        versionCode 41
        versionName "0.9.41.alpha"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "GITHUB_COMMIT", "\"${getGitHubCommit()}\""
        }
        debug {
            buildConfigField "String", "GITHUB_COMMIT", "\"${getGitHubCommit()}\""
        }
    }

    signingConfigs {
        if (file('.signing/app.prop').exists()) {
            release {
                Properties props = new Properties();
                props.load(new FileInputStream(file(".signing/app.prop")))
                storeFile file(props['RELEASE_STORE_FILE'])
                storePassword props['RELEASE_STORE_PASSWORD']
                keyAlias props['RELEASE_KEY_ALIAS']
                keyPassword props['RELEASE_KEY_PASSWORD']
            }
        }
        debug {
            if (file('.signing/app-debug.prop').exists()) {
                Properties props = new Properties();
                props.load(new FileInputStream(file(".signing/app-debug.prop")))
                storeFile file(props['RELEASE_STORE_FILE'])
                storePassword props['RELEASE_STORE_PASSWORD']
                keyAlias props['RELEASE_KEY_ALIAS']
                keyPassword props['RELEASE_KEY_PASSWORD']
            }
        }
    }
    splits {
        abi {
            enable true
            reset()
            universalApk true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'uk.openvk.android.refresh'
    lint {
        checkReleaseBuilds false
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    //noinspection GradleDependency
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0'
    implementation 'androidx.core:core-splashscreen:1.2.0'
    implementation 'androidx.annotation:annotation:1.9.1'
    //noinspection GradleDependency
    implementation 'androidx.compose.material:material:1.9.0'
    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
    implementation 'org.droidparts:droidparts:3.2.5'
    implementation 'com.github.bumptech.glide:glide:5.0.5'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.0'
    implementation 'androidx.navigation:navigation-fragment:2.9.7'
    implementation 'androidx.navigation:navigation-ui:2.9.7'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.fragment:fragment:1.8.9"
    implementation "androidx.preference:preference:1.2.1"
    annotationProcessor ('com.github.bumptech.glide:compiler:5.0.5')
    implementation 'com.github.KieronQuinn:MonetCompat:0.4.1'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'org.videolan.android:libvlc-all:3.6.5'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.2.0"
    //noinspection GradleDependency
    implementation "androidx.emoji2:emoji2:1.4.0"
    //noinspection GradleDependency
    implementation 'com.github.mancj:MaterialSearchBar:0.8.5'
    implementation 'com.github.luizgrp:SectionedRecyclerViewAdapter:v3.2.0'
}
